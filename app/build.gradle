apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "dev.projectearth.patcher"
        minSdkVersion 26
        targetSdkVersion 28 // Cant be 29 because that breaks aapt from being runnable
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def newName = outputFileName

                newName = newName.replace("app-", "${variant.mergedFlavor.applicationId}-")
                newName = newName.replace("-release", "-${variant.versionName}")
                newName = newName.replace("-debug", "-${variant.versionName}-debug")

                outputFileName = newName
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Material UI (for install steps)
    implementation 'com.google.android.material:material:1.3.0'

    // For analytics
    implementation 'com.google.firebase:firebase-analytics:18.0.2'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    // Depends for app patching
    implementation 'com.github.rtm516:Apktool:3d177ffa61'
    implementation 'ro.andob.androidawt:androidawt:1.0.4'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.56' // May not be needed
    implementation 'com.github.kellinwood.zip-signer:zipsigner-lib:2bb3b69ff3'
    implementation 'com.github.kellinwood.zip-signer:zipsigner-lib-optional:2bb3b69ff3'
    implementation 'com.github.kellinwood.zip-signer:android-sun-jarsign-support:2bb3b69ff3' // May not be needed
    //implementation 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
    //implementation 'com.github.nadavc.jgit-binary-patch:org.eclipse.jgit:07e7f95b7d' // Using this fork fixes binary patch application
    implementation 'com.github.rtm516.jgit:org.eclipse.jgit:61810ad68a' // Up-to date version of the above


    // Http requests
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // Installer steps
    implementation 'com.ernestoyaquello.stepperform:vertical-stepper-form:2.4.1'

    // Preference fixes
    //implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'com.github.rtm516.Android-Support-Preference-V7-Fix:preferencex:6b7256c57d' // Fixes the maxLength
}